# vim: syntax=python tabstop=4 expandtab
# coding: utf-8


"""
Rules for modifying SAM or BAM files.
"""



rule samtools_merge_bam:
    """
    Merge bam files for multiple units into one for the given sample.
    If the sample has only one unit, files will be copied.
    """
    input:
        lambda wildcards: expand(
            "mapped_reads/bam/{unit}_sorted.bam",
            unit=config["samples"][wildcards.sample])
    output:
        "mapped_reads/merged_samples/{sample}.bam"
    conda:
        "envs/samtools_merge_bam.yaml"
    benchmark:
        "benchmarks/samtools/merge/{sample}.txt"
    run:
        if len(input) > 1:
            shell("samtools merge {output} {input}")
        else:
            shell("cp {input} {output} && touch -h {output}")
